plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'com.vanniktech.maven.publish'
}

android {
    namespace 'com.kieronquinn.app.smartspacer.sdk.client'
    compileSdk 35

    defaultConfig {
        minSdk 29
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        buildConfigField "int", "SDK_VERSION", "2"
    }

    buildFeatures {
        viewBinding true
        aidl true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    api('com.kieronquinn.smartspacer:sdk-core:1.1.1')
    implementation('androidx.core:core-ktx:1.17.0')
    implementation('androidx.fragment:fragment-ktx:1.8.9')
    implementation('androidx.constraintlayout:constraintlayout:2.2.1')
    implementation('androidx.lifecycle:lifecycle-service:2.9.4')
    implementation('androidx.lifecycle:lifecycle-runtime-ktx:2.9.4')
    implementation('org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2')
    implementation('com.github.skydoves:balloon:1.6.13')
}

mavenPublishing {
    coordinates("com.kieronquinn.smartspacer", "sdk-client", "1.1.2")

    pom {
        name = "Smartspacer Client SDK"
        description = "SDK for showing Smartspacer Targets & Complications in your app"
        inceptionYear = "2023"
        url = "https://github.com/KieronQuinn/Smartspacer"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "KieronQuinn"
                name = "Kieron Quinn"
                url = "https://github.com/KieronQuinn"
            }
        }
        scm {
            url = "https://github.com/KieronQuinn/Smartspacer"
            connection = "scm:git:git://github.com/KieronQuinn/Smartspacer.git"
            developerConnection = "scm:git:ssh://git@github.com/KieronQuinn/Smartspacer.git"
        }
    }
}