import com.vanniktech.maven.publish.SonatypeHost

buildscript {
    ext.nav_version = "2.7.7"
    ext.protobuf_version = '0.9.0'
    ext.refine_version = '4.4.0'
    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:$protobuf_version"
        classpath "com.google.android.gms:oss-licenses-plugin:0.10.6"
    }
}

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.4.1' apply false
    id 'com.android.library' version '8.4.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.10' apply false
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin' version '2.0.1' apply false
    id 'dev.rikka.tools.refine' version "$refine_version" apply false
    id 'com.google.devtools.ksp' version '1.9.10-1.0.13' apply false
    id "com.google.gms.google-services" version "4.3.15" apply false
    id "com.google.firebase.crashlytics" version "2.9.9" apply false
    id "com.vanniktech.maven.publish" version "0.25.3" apply false
}

allprojects {
    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublishing {
            publishToMavenCentral(SonatypeHost.S01)
            signAllPublications()
        }
    }

    //JavaDoc generation is currently broken on Java 17: https://github.com/Kotlin/dokka/issues/2956
    tasks.configureEach { task ->
        if(task.name.contains("javaDocReleaseGeneration")) {
            task.enabled = false
        }
    }
}